/*******************************************************
 * SPDX-License-Identifier: GPL-2.0
 * Subsurface-Mobile own Gradle build spec. Derived from
 * the one supplied by Qt.
 *******************************************************/
buildscript {
    repositories {
        mavenCentral()
	google()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:8.2.2'
    }
}

allprojects {
    repositories {
        mavenCentral()
        maven { url 'https://jitpack.io' }
	google()
    }
}

apply plugin: 'com.android.application'

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar', '*.aar'])
    implementation 'com.github.mik3y:usb-serial-for-android:v3.10.0'
    implementation 'androidx.core:core:1.12.0'
}

android {
    /*******************************************************
     * The following variables:
     * - androidBuildToolsVersion,
     * - androidCompileSdkVersion
     * - qtAndroidDir - holds the path to qt android files
     *                  needed to build any Qt application
     *                  on Android.
     *
     * are defined in gradle.properties file. This file is
     * updated by QtCreator and androiddeployqt tools.
     * Changing them manually might break the compilation!
     *******************************************************/

    // androidCompileSdkVersion may be an integer ("35") or a platform
    // string ("android-36-ext19"); handle both forms
    if (androidCompileSdkVersion.isInteger()) {
        compileSdkVersion androidCompileSdkVersion.toInteger()
    } else {
        compileSdkVersion androidCompileSdkVersion
    }
    ndkVersion androidNdkVersion

    buildToolsVersion androidBuildToolsVersion

    namespace "org.subsurfacedivelog.mobile"

    defaultConfig {
        minSdkVersion 28
        targetSdkVersion 35
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = [qtAndroidDir + '/src', 'src', 'java']
            aidl.srcDirs = [qtAndroidDir + '/src', 'src', 'aidl']
            res.srcDirs = [qtAndroidDir + '/res', 'res']
            resources.srcDirs = ['src']
            renderscript.srcDirs = ['src']
            assets.srcDirs = ['assets']
            jniLibs.srcDirs = ['libs']
       }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    lint {
        abortOnError false
    }

    packaging {
        /* Exclude this as it is included in more than one dependency. */
        resources.excludes += 'META-INF/androidx/annotation/annotation/LICENSE.txt'
    }
}
